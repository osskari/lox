program        → declaration* EOF ;

declaration    → funDecl
               | varDecl
               | statement ;

funDecl        → "fun" function ;

function       → IDENTIFIER "(" parameters? ")" block ;

parameters     → IDENTIFIER ("," IDENTIFIER)* ;

varDecl        → "var" IDENTIFIER ( "=" expression )? ";" ;

statement      → classDecl
               | exprStmt
               | forStmt
               | ifStmt
               | printStmt
               | returnStmt
               | whileStmt
               | block ;

classDecl      → "class" IDENTIFIER "{" function* "}" ;

exprStmt       → expression ";" ;

forStmt        → "for" "(" ( varDecl | exprStmt | ";" ) expression? ";" expression? ")" statement ;

ifStmt         → "if" "(" expression ")" statement ( "else" statement )? ;

printStmt      → "print" statement ";" ;

returnStmt     → "return" expression? ";" ;

whileStmt      → "while" "(" expression ")" statement ;

block          → "{" declaration* "}"

expression     → assignment ;

assignment     → ( call "." )? IDENTIFIER "=" assignment
               | logicOr ;

equality       → comparison ( ( "!=" | "==" ) comparison )* ;

logicOr        → logicAnd ( "or" logicAnd )* ;

logicAnd       → equality ( "and" equality )*

comparison     → term ( ( ">" | ">=" | "<" | "<=" ) term )* ;

term           → factor ( ( "-" | "+" ) factor )* ;

factor         → unary ( ( "/" | "*" ) unary )* ;

unary          → ( "!" | "-" ) unary
               | call ;

call           → primary ( "(" arguments? ")" | "." IDENTIFIER )* ;

primary        → NUMBER | STRING
               | "true" | "false" | "nil"
               | "(" expression ")"
               | IDENTIFIER ;

arguments      → expression ( "," expression )*